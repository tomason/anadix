#created on: Jul 27, 2011
package org.analyzer.swingparser


import java.math.BigInteger
import org.analyzer.html.HTMLElementFactory
import org.analyzer.html.HtmlElement

global HTMLElementFactory elementFactory
global BigInteger jump

declare SimpleTagEvent
	@role (event)
end

declare StartTagEvent
	@role (event)
end

declare EndTagEvent
	@role (event)
end

rule "InsertHtml"
	salience 10
	when
		html : StartTagEvent( tagName == "html" ) from entry-point "parser"
	then 
		HtmlElement e = elementFactory.createHtmlTag(html.getId(), html.getAttributes());
		e.setPosition(html.getPosition());
		
		elementFactory.insertElement(e);
end

rule "InsertElement"
	salience -5
	when
		e : StartTagEvent( $id : id, tagName != "html" ) from entry-point "parser"
		parent : HtmlElement( id == ($id.divide(jump)) )
		
		# the element is not yet inserted
		not ( HtmlElement( id == $id ) )
	then
		HtmlElement element = elementFactory.createTag($id, e.getTagName(), parent, e.getAttributes());
		element.setPosition(e.getPosition());
		
		elementFactory.insertElement(element);
end
