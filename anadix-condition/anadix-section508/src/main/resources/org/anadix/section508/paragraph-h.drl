# Markup shall be used to associate data cells and header cells
#for data tables that have two or more logical levels of row or
#column headers. 
package org.anadix.section508

import org.anadix.html.*
import org.anadix.section508.report.*

declare MultilevelTableTag
	parent : org.anadix.html.TableTag
	child : org.anadix.html.TableTag
end

rule "multilevel data table"
	when
		DataTableTag( parent : table )
		DataTableTag( child : table)
		containedIn( child, parent; )
	then
		insert(new MultilevelTableTag(parent, child));
end

rule "asociate data cells"
	when
		tag : TdTag( headers == null, axis == null )
		MultilevelTableTag( table : parent )
		containedIn( tag, table; )
	then
		insert(new CellMissingAssociation(tag));
end
