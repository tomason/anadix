# Pages shall be designed to avoid causing the screen to flicker
#with a frequency greater than 2 Hz and lower than 55 Hz.
package org.anadix.section508

import org.anadix.ItemStatus
import org.anadix.html.*
import org.anadix.section508.report.*

rule "flickering object"
	when
		$cause : ObjectTag( )
	then
		insert(new ElementCauseFlickering($cause));
end

rule "flickering applet"
	when
		$cause : AppletTag( )
	then
		insert(new ElementCauseFlickering($cause));
end

rule "flickering image"
	when
		$cause : ImgTag( src != null && str[endsWith] "gif" )
	then
		insert(new ElementCauseFlickering($cause));
end

rule "warn about blink and marquee"
	when
		$cause : HtmlElement( name == "blink" || == "marquee" )
	then
		insert(new ElementCauseFlickering(ItemStatus.WARNING, "element.cause.flickering", $cause));
end

rule "recomend manual check"
	when
	then
		insert(new ElementCauseFlickering());
end

// there are also css ways to do marquee: overflow-style and marquee-speed, but I suppose
//these comply with Section 508 requirements
