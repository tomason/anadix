# A text equivalent for every non-text element shall be provided
#(e.g., via "alt", "longdesc", or in element content). 
package org.anadix.section508

import org.anadix.html.*
import org.anadix.section508.report.*

rule "img without alt and longdesc"
    when
        $element : ImgTag( alt == null && longdesc == null )
    then
    	insert(new MissingAlt($element));
end

rule "input without alt"
	when
		$element : InputTag( type != "hidden", alt == null )
	then
		insert(new MissingAlt($element));
end

rule "object without text content"
	when
		$element : ObjectTag( textContent == null )
	then
		insert(new MissingAlt($element));
end

rule "applet without alt and text content"
	when
		$element : AppletTag( alt == null || textContent == null )
	then
		insert(new MissingAlt($element));
end

rule "iframe without text content"
	when
		$element : IframeTag( textContent == null )
	then
		insert(new MissingAlt($element));
end

rule "area without alt"
	when
		$element : AreaTag( alt == null )
	then
		insert(new MissingAlt($element));
end

rule "decorative img"
	when
		$element : ImgTag( alt == "" )
	then
		insert(new ImgEmptyAlt($element));
end
