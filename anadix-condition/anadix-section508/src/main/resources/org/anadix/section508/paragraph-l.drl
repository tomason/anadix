# When pages utilize scripting languages to display content, or to create
#interface elements, the information provided by the script shall be
#identified with functional text that can be read by assistive technology. 
package org.anadix.section508

import org.anadix.html.*

rule "missing noscript tag"
	salience -10
	when
		exists (
			ScriptTag( )
		)
		not (
			NoscriptTag( textContent != null )
		)
	then
		insertLogical(ReportFactory.newNoscriptMissingReport());
end

rule "anchor uses javascript"
	when
		$element : ATag( href matches "javascript:.*" )
	then
		insert(ReportFactory.newHrefJavascriptReport($element));
end

rule "area uses javascript"
	when
		$element : AreaTag( href matches "javascript:.*" )
	then
		insert(ReportFactory.newHrefJavascriptReport($element));
end
