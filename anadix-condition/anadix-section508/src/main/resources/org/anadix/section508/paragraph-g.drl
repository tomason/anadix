# Row and column headers shall be identified for data tables. 
package org.anadix.section508

import org.anadix.html.*
import org.anadix.section508.report.*

declare RowWithoutHeader
	row : TrTag
end

declare ColumnCount
	table : org.anadix.html.TableTag
	row : TrTag
	th : int
	td : int
end

// rows without headers
rule "row without header cell"
	salience 100
	when
		$row : TrTag( )
		not (
			ThTag( parent == $row )
		)
	then
		//insertLogical(new RowWithoutHeader($row));
		RowWithoutHeader rwh = new RowWithoutHeader();
		rwh.setRow($row);
		insertLogical(rwh);
end

rule "row without header in data table"
	when
		DataTableTag( $table; )
		RowWithoutHeader( $row; )
		containedIn( $row, $table; )
	then
		insert(new RowMissingHeader($row));
end


// columns without headers
rule "insert column count for tables"
	when
		DataTableTag( $table; )
		not (
			ColumnCount( table == $table )
		)
	then
		//insert(new ColumnCount($table, (TrTag)null, 0, 0));
		ColumnCount cc = new ColumnCount();
		cc.setTable($table);
		cc.setTd(0);
		cc.setTh(0);
		
		insert(cc);
end

rule "insert column count for rows"
	when
		$row : TrTag( )
		not (
			ColumnCount( row == $row )
		)
	then
		//insert(new ColumnCount((TableTag)null, $row, 0, 0));
		ColumnCount cc = new ColumnCount();
		cc.setRow($row);
		cc.setTd(0);
		cc.setTh(0);
		
		insert(cc);
end


rule "count data columns in row"
	when
		$count : ColumnCount( $row : row != null, $old : td )
		Number( $new : intValue > $old ) from accumulate (
			$tag : TdTag( parent == $row ),
			count($tag)
		)
	then
		modify ($count) {
			setTd($new);
		}
end

rule "count header columns in row"
	when
		$count : ColumnCount( $row : row != null, $old : th )
		Number( $new : intValue > $old ) from accumulate (
			$tag : ThTag( parent == $row ),
			count($tag)
		)
	then
		modify ($count) {
			setTh($new);
		}
end

rule "max header columns in table"
	when
		$count : ColumnCount( $table : table != null, $old : th )
		ColumnCount( $row : row != null, td == 0, $value : th > $old)
		containedIn( $row, $table; )
	then
		modify ($count) {
			setTh($value);
		}
end

rule "max data columns in table"
	when
		$count : ColumnCount( $table : table != null, $old : td )
		Number( $new : intValue > $old) from accumulate (
			( and
				ColumnCount( $row : row != null, $td : td != 0, $th : th)
				containedIn( $row, $table; )
			),
			max($td + $th)
		)
	then
		modify ($count) {
			setTd($new);
		}
end

rule "compare data and headers count"
	when
		ColumnCount( $table : table != null, $td : td, $th : th )
		eval($td > $th)
	then
		insert(new ColumnMissingHeader($table));
end
