/*
Copyright 2011 Tomas Schlosser

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
package org.anadix.section508

import org.anadix.html.*
import org.anadix.section508.report.*

declare RowWithoutHeader
	row : TrTag
end

declare ColumnCount
	table : org.anadix.html.TableTag
	row : TrTag
	th : int
	td : int
end

// rows without headers
rule "row without header cell"
	salience 100
	when
		$row : TrTag( )
		not (
			ThTag( parent == $row )
		)
	then
		insertLogical(new RowWithoutHeader($row));
end

rule "row without header in data table"
	when
		DataTableTag( $table; )
		RowWithoutHeader( $row; )
		containedIn( $row, $table; )
	then
		insert(new RowMissingHeader($row));
end


// columns without headers
rule "insert column count for tables"
	salience 1000
	when
		DataTableTag( $table; )
		not (
			ColumnCount( table == $table )
		)
	then
		insert(new ColumnCount($table, null, 0, 0));
end

rule "insert column count for rows"
	salience 1000
	when
		$row : TrTag( )
		not (
			ColumnCount( row == $row )
		)
	then
		insert(new ColumnCount(null, $row, 0, 0));
end


rule "count data columns in row"
	salience 100
	when
		$count : ColumnCount( $row : row != null, $old : td )
		Number( $new : intValue > $old ) from accumulate (
			$tag : TdTag( parent == $row ),
			count($tag)
		)
	then
		modify ($count) {
			setTd($new);
		}
end

rule "count header columns in row"
	salience 100
	when
		$count : ColumnCount( $row : row != null, $old : th )
		Number( $new : intValue > $old ) from accumulate (
			$tag : ThTag( parent == $row ),
			count($tag)
		)
	then
		modify ($count) {
			setTh($new);
		}
end

rule "max header columns in table"
	salience 10
	when
		$count : ColumnCount( $table : table != null, $old : th )
		ColumnCount( $row : row != null, td == 0, $new : th > $old)
		containedIn( $row, $table; )
	then
		modify ($count) {
			setTh($new);
		}
end

rule "max data columns in table"
	salience 10
	when
		$count : ColumnCount( $table : table != null, $old : td )
		ColumnCount( $row : row != null, $new : (td + th) > $old)
		containedIn( $row, $table; )
	then
		modify ($count) {
			setTd($new);
		}
end

rule "compare data and headers count"
	when
		ColumnCount( $table : table != null, $td : td, $th : th )
		eval($td > $th)
	then
		insert(new ColumnMissingHeader($table));
end
