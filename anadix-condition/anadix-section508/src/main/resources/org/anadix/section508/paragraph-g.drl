# Row and column headers shall be identified for data tables. 
package org.anadix.section508

import org.anadix.html.*
import org.anadix.section508.report.*

declare RowWithoutHeader
	row : TrTag
end

declare ColumnCount
	table : TableTag
	count : int
end

// rows without headers
rule "row without header cell"
	salience 100
	when
		$row : TrTag( )
		not (
			ThTag( parent == $row )
		)
	then
		insertLogical(new RowWithoutHeader($row));
end

rule "row without header in data table"
	when
		DataTableTag( $table : table )
		RowWithoutHeader( $row : row )
		containedIn( $row, $table; )
	then
		insert(new RowMissingHeader($row));
end


// columns without headers
rule "insert column count"
	when
		DataTableTag( $table : table )
		not (
			ColumnCount( table == $table )
		)
	then
		insert(new ColumnCount($table, 0));
end

rule "count columns"
	when
		$count : ColumnCount( $table : table )
		$row : TrTag( parent == $table )
		containedIn( $row, $table; )
		accumulate (
			( and
				$tag : TdTag( )
				containedIn($tag, $row;)
			),
			$tds : count( $tag )
		)
		accumulate (
			( and
				$tag : ThTag( )
				containedIn($tag, $row;)
			),
			$ths : count( $tag )
		)
		eval ($count.getCount() < ($tds.intValue() + $ths.intValue()))
	then
		modify ($count) {
			setCount($tds.intValue() + $ths.intValue());
		}
end

rule "count headers and compare"
	when
		ColumnCount( $table : table, $count : count )
		$row : TrTag( )
		containedIn( $row, $table; )
		not (
			$data : TdTag( ) and
			containedIn( $data, $row; )
		)
		accumulate (
			( and
				$tag : ThTag( )
				containedIn( $tag, $table; )
			),
			$headers : count( $tag )
		)
		eval($count > $headers.intValue())
	then
		insert(new ColumnMissingHeader($table));
end
